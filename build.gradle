plugins {
    id 'java'
    id 'maven-publish'
    id 'io.github.mcengine.gradle.plugin.version-calculator' version '1.0.0'
}

group = project.property('project-group')

def artifactId = project.findProperty('project-artifact-id') ?: 'mcbackpack-api'
def artifactName = project.findProperty('project-artifact-name') ?: 'MCBackpack API'
def artifactDescription = project.findProperty('project-artifact-description') ?: "Public interfaces for the MCBackpack ecosystem."
def gitOwner = project.findProperty('git-org-name') ?: System.getenv('GIT_ORG_NAME') ?: 'MCValac'
def gitRepo = project.findProperty('git-org-repository') ?: System.getenv('GIT_ORG_REPO') ?: 'plugin-api'
def gitUrl = "https://github.com/${gitOwner}/${gitRepo}"

println "[${project.name}] Final Version (via Plugin): ${project.version}"

repositories {
    mavenCentral()
}

publishing {
    publications {
        create('mavenJava', MavenPublication) {
            artifactId = artifactId 
            from components.java

            pom {
                name = artifactName
                description = artifactDescription
                url = gitUrl

                developers {
                    developer {
                        id = 'jetsadawijit'
                        url = 'https://jetsadawijit.github.io'
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/${gitOwner}/${gitRepo}.git"
                    developerConnection = "scm:git:ssh://github.com/${gitOwner}/${gitRepo}.git"
                    url = gitUrl
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/${gitOwner}/${gitRepo}")

            credentials {
                username = System.getenv('GITHUB_ACTOR') ?: System.getenv('AGENT_NAME') ?: System.getenv('USER_GITHUB_NAME')
                password = System.getenv('GITHUB_TOKEN') ?: System.getenv('AGENT_TOKEN') ?: System.getenv('USER_GITHUB_TOKEN')
            }
        }
    }
}
